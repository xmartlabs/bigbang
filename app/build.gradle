apply from: '../versions.gradle'

buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
        classpath 'com.google.gms:google-services:3.1.1'
        classpath 'io.fabric.tools:gradle:1.24.3'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.github.ben-manes.versions'
//apply plugin: 'io.fabric' // TODO: uncomment when having set the Fabric API key
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def getSignInformation = { keystorePath, keystorePropertyPath ->
    def keyStore = file(keystorePath)
    if (!keyStore.exists()) {
        logger.error("Keystore file not found, path=${keyStore.absolutePath}")
        return
    }
    def propertiesFile = file(keystorePropertyPath)
    if (!propertiesFile.exists()) {
        logger.error("Keystore properties file not found, path=${propertiesFile.absolutePath}")
        return
    }
    def Properties properties = new Properties()
    properties.load(new FileInputStream(propertiesFile))
    return [
            storeFile    : keyStore,
            storePassword: properties["store_password"],
            keyAlias     : properties["key_alias"],
            keyPassword  : properties["key_password"]
    ]
}

android {
    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion project.ext.buildToolsVersion
    defaultConfig {
        applicationId 'com.xmartlabs.template'
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode project.ext.librariesVersionCode
        versionName project.ext.librariesVersion
    }
    signingConfigs {
        //noinspection GroovyMissingReturnStatement
        debug {
            def signInformation = getSignInformation("debug.keystore", "keystore.properties")
            if (signInformation != null) {
                storeFile signInformation.storeFile
                storePassword signInformation.storePassword
                keyAlias signInformation.keyAlias
                keyPassword signInformation.keyPassword
            }
        }
        //noinspection GroovyMissingReturnStatement
        release {
            def signInformation = getSignInformation("release.keystore", "keystore.properties")
            if (signInformation != null) {
                storeFile signInformation.storeFile
                storePassword signInformation.storePassword
                keyAlias signInformation.keyAlias
                keyPassword signInformation.keyPassword
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            //noinspection GrUnresolvedAccess
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            //noinspection GrUnresolvedAccess,GroovyAssignabilityCheck
            //signingConfig signingConfigs.release
        }
    }
    flavorDimensions 'version', 'env'
    productFlavors {
        //noinspection GroovyMissingReturnStatement
        production {
            dimension 'env'
        }

        staging {
            dimension 'env'
            applicationIdSuffix '.staging'
        }

        lollipopAndAbove {
            dimension 'version'
            minSdkVersion 21 // For faster multidex compilation
        }

        allVersions {
            dimension 'version'
        }
    }
    //noinspection GroovyMissingReturnStatement
    lintOptions {
        checkAllWarnings true
        ignore 'GradleDependency', 'NewerVersionAvailable'
        warningsAsErrors false
        abortOnError false
    }
    packagingOptions {
        exclude '.readme'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    buildToolsVersion project.ext.buildToolsVersion
}

dependencies {
    implementation "com.android.support:appcompat-v7:${libraryVersion.androidSupport}"
    implementation "com.android.support:cardview-v7:${libraryVersion.androidSupport}"
    implementation "com.android.support:design:${libraryVersion.androidSupport}"
    implementation "com.android.support:recyclerview-v7:${libraryVersion.androidSupport}"
    implementation "com.android.support:support-v13:${libraryVersion.androidSupport}"
    implementation "com.f2prateek.dart:dart:${libraryVersion.dart}"
    implementation "com.f2prateek.dart:henson:${libraryVersion.dart}"
    implementation "com.github.mrmike:Ok2Curl:${libraryVersion.ok2Curl}"
    implementation "com.github.Raizlabs.DBFlow:dbflow-core:${libraryVersion.dbFlow}"
    implementation "com.github.Raizlabs.DBFlow:dbflow:${libraryVersion.dbFlow}"
    implementation "com.github.tbroyer.bullet:bullet:${libraryVersion.bullet}"
    implementation "com.github.xmartlabs:Traceur:${libraryVersion.traceur}"
    implementation "com.google.dagger:dagger:${libraryVersion.dagger}"
    implementation "com.hannesdorfmann.fragmentargs:annotation:${libraryVersion.fragmentArgs}"
    implementation "com.hannesdorfmann.fragmentargs:bundler-parceler:${libraryVersion.fragmentArgs}"
    implementation "com.jakewharton.threetenabp:threetenabp:${libraryVersion.localDateBackport}"
    implementation "com.jakewharton.timber:timber:${libraryVersion.timber}"
    implementation "com.squareup.okhttp3:logging-interceptor:${libraryVersion.okHttp}"
    implementation "com.squareup.okhttp3:okhttp:${libraryVersion.okHttp}"
    implementation "com.squareup.okio:okio:${libraryVersion.okio}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${libraryVersion.retrofit}"
    implementation "com.squareup.retrofit2:converter-gson:${libraryVersion.retrofit}"
    implementation "com.squareup.retrofit2:retrofit:${libraryVersion.retrofit}"
    implementation "com.trello.rxlifecycle2:rxlifecycle-components:${libraryVersion.rxLifecycle}"
    implementation "com.trello.rxlifecycle2:rxlifecycle:${libraryVersion.rxLifecycle}"
    implementation "com.wdullaer:materialdatetimepicker:${libraryVersion.materialDateTimePicker}"
    implementation "io.reactivex.rxjava2:rxandroid:${libraryVersion.rxAndroid}"
    implementation "io.reactivex.rxjava2:rxjava:${libraryVersion.rxJava}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:${libraryVersion.kotlin}"
    implementation "org.parceler:parceler-api:${libraryVersion.parceler}"
    implementation 'com.github.square.picasso:picasso:8c16e8564e'
    implementation 'com.viewpagerindicator:library:2.4.1'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(path: ':core')
    implementation project(path: ':log-crashlytics')
    implementation project(path: ':retrofit')
    implementation project(path: ':ui')

    kapt "com.f2prateek.dart:dart-processor:${libraryVersion.dart}"
    kapt "com.f2prateek.dart:henson-processor:${libraryVersion.dart}"
    kapt "com.github.Raizlabs.DBFlow:dbflow-processor:${libraryVersion.dbFlow}"
    kapt "com.github.tbroyer.bullet:bullet-compiler:${libraryVersion.bullet}"
    kapt "com.google.dagger:dagger-compiler:${libraryVersion.dagger}"
    kapt "com.hannesdorfmann.fragmentargs:processor:${libraryVersion.fragmentArgs}"
    kapt "org.parceler:parceler:${libraryVersion.parceler}"

    compileOnly 'javax.annotation:jsr250-api:1.0'
}

//apply plugin: 'com.google.gms.google-services' // TODO: enable when adding google-services.json
