buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.12.0'
        classpath 'com.google.gms:google-services:3.0.0'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'io.fabric.tools:gradle:1.21.7'
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.jakewharton.hugo'
//apply plugin: 'io.fabric' // TODO: uncomment when having set the Fabric API key
apply plugin: 'me.tatarka.retrolambda'

repositories {
    flatDir {
        dirs 'libs'
    }
    maven { url "https://jitpack.io" }
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.0'
    defaultConfig {
        applicationId 'com.xmartlabs.template'
        minSdkVersion 17
        targetSdkVersion 24
        versionCode 1
        versionName '0.1.0'
    }
    signingConfigs {
        debug {
            def debugKeyStore = file('debug.keystore')
            if (debugKeyStore.exists()) {
                storeFile debugKeyStore
            } else {
                //noinspection GrUnresolvedAccess
                logger.error('Debug keystore file not found')
            }
        }
        release {
            def releaseKeyStore = file('release.keystore')
            if (releaseKeyStore.exists()) {
                storeFile releaseKeyStore
            } else {
                //noinspection GrUnresolvedAccess
                logger.error('Release keystore file not found')
            }
        }

        def Properties properties = new Properties()
        def propertiesFile = file('keystore.properties')
        if (propertiesFile.exists()) {
            properties.load(new FileInputStream(propertiesFile))
            //noinspection GrUnresolvedAccess
            storePassword properties['store_password']
            //noinspection GrUnresolvedAccess
            keyAlias properties['key_alias']
            //noinspection GrUnresolvedAccess
            keyPassword properties['key_password']
        } else {
            //noinspection GrUnresolvedAccess
            logger.error('Keystore properties file not found')
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            //noinspection GrUnresolvedAccess
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            //noinspection GrUnresolvedAccess,GroovyAssignabilityCheck
            signingConfig signingConfigs.release
        }
    }
    productFlavors {
        //noinspection GroovyMissingReturnStatement
        production {

        }
        staging {
            applicationIdSuffix '.staging'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    //noinspection GroovyMissingReturnStatement
    lintOptions {
        disable 'InvalidPackage'
    }
    packagingOptions {
        exclude '.readme'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

final ANDROID_SUPPORT_VERSION = '24.0.0'
final BULLET_VERSION = 'e5044b53df'
final BUTTER_KNIFE_VERSION = '8.1.0'
final DAGGER_VERSION = '2.5'
final DART_VERSION = '2.0.0'
final DB_FLOW_VERSION = '3.0.1'
final FRAGMENT_ARGS_VERSION = '3.0.2'
final LOMBOK_VERSION = '1.16.8'
final OK_HTTP_VERSION = '3.3.1'
final OKIO_VERSION = '1.8.0'
final PARCELER_VERSION = '1.1.5'
final RETROFIT_VERSION = '2.1.0'
final RX_LIFECYCLE_VERSION = '0.6.1'
final FIREBASE_VERSION = '9.0.2'

dependencies {
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.5.4'
    androidTestCompile 'org.mockito:mockito-core:2.0.70-beta'
    apt "com.f2prateek.dart:dart-processor:${DART_VERSION}"
    apt "com.f2prateek.dart:henson-processor:${DART_VERSION}"
    apt "com.github.Raizlabs.DBFlow:dbflow-processor:${DB_FLOW_VERSION}"
    apt "com.github.tbroyer.bullet:bullet-compiler:${BULLET_VERSION}"
    provided "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"
    apt "com.hannesdorfmann.fragmentargs:processor:${FRAGMENT_ARGS_VERSION}"
    apt "com.jakewharton:butterknife-compiler:${BUTTER_KNIFE_VERSION}"
    //apt 'net.ltgt.dagger:bullet-compiler:1.0-SNAPSHOT' // Loaded from JitPack
    apt "org.parceler:parceler:${PARCELER_VERSION}"
    apt "org.projectlombok:lombok:${LOMBOK_VERSION}"
    compile "com.android.support:appcompat-v7:${ANDROID_SUPPORT_VERSION}"
    compile "com.android.support:cardview-v7:${ANDROID_SUPPORT_VERSION}"
    compile "com.android.support:design:${ANDROID_SUPPORT_VERSION}"
    compile "com.android.support:recyclerview-v7:${ANDROID_SUPPORT_VERSION}"
    compile "com.android.support:support-v13:${ANDROID_SUPPORT_VERSION}"
    compile 'com.annimon:stream:1.0.9'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.7@aar') {
        transitive = true
    }
    compile "com.f2prateek.dart:dart:${DART_VERSION}"
    compile "com.f2prateek.dart:henson:${DART_VERSION}"
    compile 'com.github.mrmike:Ok2Curl:0.2.3'
    compile "com.github.Raizlabs.DBFlow:dbflow-core:${DB_FLOW_VERSION}"
    compile "com.github.Raizlabs.DBFlow:dbflow:${DB_FLOW_VERSION}"
    compile 'com.github.square.picasso:picasso:8c16e8564e'
    compile "com.github.tbroyer.bullet:bullet:${BULLET_VERSION}"
    compile "com.google.dagger:dagger:${DAGGER_VERSION}"
    compile "com.google.firebase:firebase-core:${FIREBASE_VERSION}"
    compile "com.google.firebase:firebase-invites:${FIREBASE_VERSION}"
    compile "com.google.firebase:firebase-messaging:${FIREBASE_VERSION}"
    compile "com.hannesdorfmann.fragmentargs:annotation:${FRAGMENT_ARGS_VERSION}"
    compile "com.hannesdorfmann.fragmentargs:bundler-parceler:${FRAGMENT_ARGS_VERSION}"
    compile "com.jakewharton:butterknife:${BUTTER_KNIFE_VERSION}"
    compile 'com.jakewharton.timber:timber:4.1.2'
    compile "com.squareup.okhttp3:okhttp:${OK_HTTP_VERSION}"
    compile "com.squareup.okhttp3:logging-interceptor:${OK_HTTP_VERSION}"
    compile "com.squareup.okio:okio:${OKIO_VERSION}"
    //compile 'com.squareup.picasso:picasso:2.5.2' // Loaded from JitPack in order to use OkHttp3
    compile "com.squareup.retrofit2:converter-gson:${RETROFIT_VERSION}"
    compile "com.squareup.retrofit2:adapter-rxjava:${RETROFIT_VERSION}"
    compile "com.squareup.retrofit2:retrofit:${RETROFIT_VERSION}"
    compile "com.trello:rxlifecycle:${RX_LIFECYCLE_VERSION}"
    compile "com.trello:rxlifecycle-components:${RX_LIFECYCLE_VERSION}"
    compile 'com.viewpagerindicator:library:2.4.1@aar'
    compile 'io.reactivex:rxandroid:1.2.1@aar'
    compile 'io.reactivex:rxjava:1.1.6'
    //compile 'net.ltgt.dagger:bullet:1.0-SNAPSHOT' // Loaded from JitPack
    compile "org.parceler:parceler-api:${PARCELER_VERSION}"
    compile fileTree(include: ['*.jar'], dir: 'libs')
    provided "org.projectlombok:lombok:${LOMBOK_VERSION}"
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.1'
}

apply plugin: 'com.google.gms.google-services'

/*apply plugin: 'com.google.gms.google-services'

def appModuleRootFolder = '.'
def srcDir = 'src'
def googleServicesJsonFile = 'google-services.json'

task switchToStaging(type: Copy) {
    //noinspection GrUnresolvedAccess
    outputs.upToDateWhen { false }
    def flavor = 'staging'
    from "${srcDir}/$flavor/$googleServicesJsonFile"
    into "$appModuleRootFolder"
}

task switchToProduction(type: Copy) {
    //noinspection GrUnresolvedAccess
    outputs.upToDateWhen { false }
    def flavor = 'production'
    from "${srcDir}/$flavor/$googleServicesJsonFile"
    into "$appModuleRootFolder"
}

afterEvaluate {
    //noinspection GrUnresolvedAccess
    processStagingDebugGoogleServices.dependsOn switchToStaging
    //noinspection GrUnresolvedAccess
    processStagingReleaseGoogleServices.dependsOn switchToStaging
    //noinspection GrUnresolvedAccess
    processProductionDebugGoogleServices.dependsOn switchToProduction
    //noinspection GrUnresolvedAccess
    processProductionReleaseGoogleServices.dependsOn switchToProduction
}*/
