apply from: '../versions.gradle'

buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
        classpath 'com.google.gms:google-services:3.0.0'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'io.fabric.tools:gradle:1.21.7'
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.jakewharton.hugo'
//apply plugin: 'io.fabric' // TODO: uncomment when having set the Fabric API key
apply plugin: 'me.tatarka.retrolambda'

repositories {
    flatDir {
        dirs 'libs'
    }
    maven { url "https://jitpack.io" }
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

def getSignInformation = { keystorePath, keystorePropertyPath ->
    def keyStore = file(keystorePath)
    if (!keyStore.exists()) {
        logger.error("Keystore file not found, path=${keyStore.absolutePath}")
        return
    }
    def propertiesFile = file(keystorePropertyPath)
    if (!propertiesFile.exists()) {
        logger.error("Keystore properties file not found, path=${propertiesFile.absolutePath}")
        return
    }
    def Properties properties = new Properties()
    properties.load(new FileInputStream(propertiesFile))
    return [
            storeFile    : keyStore,
            storePassword: properties["store_password"],
            keyAlias     : properties["key_alias"],
            keyPassword  : properties["key_password"]
    ]
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    defaultConfig {
        applicationId 'com.xmartlabs.template'
        minSdkVersion 17
        targetSdkVersion 25
        testInstrumentationRunner "${applicationId}.TestRunner"
        versionCode 1
        versionName '0.1.0'
    }
    signingConfigs {
        //noinspection GroovyMissingReturnStatement
        debug {
            def signInformation = getSignInformation("debug.keystore", "keystore.properties")
            if (signInformation != null) {
                storeFile signInformation.storeFile
                storePassword signInformation.storePassword
                keyAlias signInformation.keyAlias
                keyPassword signInformation.keyPassword
            }
        }
        //noinspection GroovyMissingReturnStatement
        release {
            def signInformation = getSignInformation("release.keystore", "keystore.properties")
            if (signInformation != null) {
                storeFile signInformation.storeFile
                storePassword signInformation.storePassword
                keyAlias signInformation.keyAlias
                keyPassword signInformation.keyPassword
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            //noinspection GrUnresolvedAccess
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            //noinspection GrUnresolvedAccess,GroovyAssignabilityCheck
            signingConfig signingConfigs.release
        }
    }
    flavorDimensions 'version', 'env'
    productFlavors {
        //noinspection GroovyMissingReturnStatement
        production {
            dimension 'env'
        }

        staging {
            dimension 'env'
            applicationIdSuffix '.staging'
        }

        lollipopAndAbove {
            dimension 'version'
            minSdkVersion 21 // For faster multidex compilation
        }

        allVersions {
            dimension 'version'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    //noinspection GroovyMissingReturnStatement
    lintOptions {
        disable 'InvalidPackage'
    }
    packagingOptions {
        exclude '.readme'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    androidTestApt "com.github.tbroyer.bullet:bullet-compiler:${project.ext.bulletVersion}"
    androidTestApt "com.google.dagger:dagger-compiler:${project.ext.daggerVersion}"
    androidTestCompile "com.android.support:support-annotations:${project.ext.androidSupportVersion}"
    androidTestCompile "com.android.support.test:rules:${project.ext.androidSupportTestVersion}"
    androidTestCompile("com.android.support.test:runner:${project.ext.androidSupportTestVersion}") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:${project.ext.expressoVersion}") {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude module: 'recyclerview-v7'
    }
    androidTestCompile("com.android.support.test.espresso:espresso-core:${project.ext.expressoVersion}") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile "com.android.support.test.espresso:espresso-intents:${project.ext.expressoVersion}"
    androidTestCompile "com.github.andrzejchm.RESTMock:android:${project.ext.restMockVersion}"
    androidTestCompile("com.github.andrzejchm.RESTMock:core:${project.ext.restMockVersion}") {
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
    }
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    androidTestCompile "com.crittercism.dexmaker:dexmaker:${project.ext.dexmakerVersion}"
    androidTestCompile "com.crittercism.dexmaker:dexmaker-dx:${project.ext.dexmakerVersion}"
    androidTestCompile "com.crittercism.dexmaker:dexmaker-mockito:${project.ext.dexmakerVersion}"
    androidTestCompile "com.google.code.findbugs:jsr305:${project.ext.jsr305Version}" // Added to prevent dependencies conflict between main and test
    androidTestCompile 'com.jakewharton.espresso:okhttp3-idling-resource:1.0.0'
    androidTestCompile "com.squareup.okhttp3:okhttp:${project.ext.okHttpVersion}" // Added to prevent dependencies conflict between main and test
    androidTestCompile "com.squareup.okhttp3:mockwebserver:${project.ext.okHttpVersion}" // Added to prevent dependencies conflict between main and test
    androidTestCompile "com.squareup.okio:okio:${project.ext.okioVersion}" // Added to prevent dependencies conflict between main and test
    androidTestCompile "org.mockito:mockito-core:${project.ext.mockitoVersion}"
    apt "com.f2prateek.dart:dart-processor:${project.ext.dartVersion}"
    apt "com.f2prateek.dart:henson-processor:${project.ext.dartVersion}"
    apt "com.github.Raizlabs.DBFlow:dbflow-processor:${project.ext.dbFlowVersion}"
    apt "com.github.tbroyer.bullet:bullet-compiler:${project.ext.bulletVersion}"
    provided "com.google.dagger:dagger-compiler:${project.ext.daggerVersion}"
    apt "com.hannesdorfmann.fragmentargs:processor:${project.ext.fragmentArgsVersion}"
    apt "com.jakewharton:butterknife-compiler:${project.ext.butterKnifeVersion}"
    //apt 'net.ltgt.dagger:bullet-compiler:1.0-SNAPSHOT' // Loaded from JitPack
    apt "org.parceler:parceler:${project.ext.parcelerVersion}"
    apt "org.projectlombok:lombok:${project.ext.lombokVersion}"
    compile "com.android.support:appcompat-v7:${project.ext.androidSupportVersion}"
    compile "com.android.support:cardview-v7:${project.ext.androidSupportVersion}"
    compile "com.android.support:design:${project.ext.androidSupportVersion}"
    compile "com.android.support:recyclerview-v7:${project.ext.androidSupportVersion}"
    compile "com.android.support:support-v13:${project.ext.androidSupportVersion}"
    compile "com.annimon:stream:${project.ext.streamVersion}"
    compile('com.crashlytics.sdk.android:crashlytics:2.6.1@aar') {
        transitive = true
    }
    compile "com.f2prateek.dart:dart:${project.ext.dartVersion}"
    compile "com.f2prateek.dart:henson:${project.ext.dartVersion}"
    compile 'com.github.mrmike:Ok2Curl:0.2.5'
    compile "com.github.Raizlabs.DBFlow:dbflow-core:${project.ext.dbFlowVersion}"
    compile "com.github.Raizlabs.DBFlow:dbflow:${project.ext.dbFlowVersion}"
    compile 'com.github.square.picasso:picasso:8c16e8564e'
    compile "com.github.tbroyer.bullet:bullet:${project.ext.bulletVersion}"
    compile "com.google.dagger:dagger:${project.ext.daggerVersion}"
    compile "com.hannesdorfmann.fragmentargs:annotation:${project.ext.fragmentArgsVersion}"
    compile "com.hannesdorfmann.fragmentargs:bundler-parceler:${project.ext.fragmentArgsVersion}"
    compile "com.jakewharton:butterknife:${project.ext.butterKnifeVersion}"
    compile "com.jakewharton.threetenabp:threetenabp:${project.ext.localDateBackportVersion}"
    compile "com.jakewharton.timber:timber:${project.ext.timberVersion}"
    compile "com.squareup.okhttp3:okhttp:${project.ext.okHttpVersion}"
    compile "com.squareup.okhttp3:logging-interceptor:${project.ext.okHttpVersion}"
    compile "com.squareup.okio:okio:${project.ext.okioVersion}"
    //compile 'com.squareup.picasso:picasso:2.5.2' // Loaded from JitPack in order to use OkHttp3
    compile "com.squareup.retrofit2:converter-gson:${project.ext.retrofitVersion}"
    compile "com.squareup.retrofit2:adapter-rxjava2:${project.ext.retrofitVersion}"
    compile "com.squareup.retrofit2:retrofit:${project.ext.retrofitVersion}"
    compile "com.trello.rxlifecycle2:rxlifecycle:${project.ext.rxLifecycleVersion}"
    compile "com.trello.rxlifecycle2:rxlifecycle-components:${project.ext.rxLifecycleVersion}"
    compile 'com.viewpagerindicator:library:2.4.1@aar'
    compile 'com.wdullaer:materialdatetimepicker:2.5.0'
    compile "io.reactivex.rxjava2:rxandroid:${project.ext.rxAndroidVersion}"
    compile "io.reactivex.rxjava2:rxjava:${project.ext.rxJavaVersion}"
    //compile 'net.ltgt.dagger:bullet:1.0-SNAPSHOT' // Loaded from JitPack
    compile "org.parceler:parceler-api:${project.ext.parcelerVersion}"
    compile fileTree(include: ['*.jar'], dir: 'libs')
    provided "org.projectlombok:lombok:${project.ext.lombokVersion}"
    testCompile 'junit:junit:4.12'
    compile project(path: ':core')
}

//apply plugin: 'com.google.gms.google-services' // TODO: enable when adding google-services.json

apply from: 'circleci.gradle'
