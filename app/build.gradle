apply from: '../versions.gradle'

buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
        classpath 'com.google.gms:google-services:3.1.0'
        classpath 'io.fabric.tools:gradle:1.21.7'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.github.ben-manes.versions'
//apply plugin: 'io.fabric' // TODO: uncomment when having set the Fabric API key
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def getSignInformation = { keystorePath, keystorePropertyPath ->
    def keyStore = file(keystorePath)
    if (!keyStore.exists()) {
        logger.error("Keystore file not found, path=${keyStore.absolutePath}")
        return
    }
    def propertiesFile = file(keystorePropertyPath)
    if (!propertiesFile.exists()) {
        logger.error("Keystore properties file not found, path=${propertiesFile.absolutePath}")
        return
    }
    def Properties properties = new Properties()
    properties.load(new FileInputStream(propertiesFile))
    return [
            storeFile    : keyStore,
            storePassword: properties["store_password"],
            keyAlias     : properties["key_alias"],
            keyPassword  : properties["key_password"]
    ]
}

android {
    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion project.ext.buildToolsVersion
    defaultConfig {
        applicationId 'com.xmartlabs.template'
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        testInstrumentationRunner "${applicationId}.TestRunner"
        versionCode project.ext.librariesVersionCode
        versionName project.ext.librariesVersion
    }
    signingConfigs {
        //noinspection GroovyMissingReturnStatement
        debug {
            def signInformation = getSignInformation("debug.keystore", "keystore.properties")
            if (signInformation != null) {
                storeFile signInformation.storeFile
                storePassword signInformation.storePassword
                keyAlias signInformation.keyAlias
                keyPassword signInformation.keyPassword
            }
        }
        //noinspection GroovyMissingReturnStatement
        release {
            def signInformation = getSignInformation("release.keystore", "keystore.properties")
            if (signInformation != null) {
                storeFile signInformation.storeFile
                storePassword signInformation.storePassword
                keyAlias signInformation.keyAlias
                keyPassword signInformation.keyPassword
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            //noinspection GrUnresolvedAccess
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            //noinspection GrUnresolvedAccess,GroovyAssignabilityCheck
            signingConfig signingConfigs.release
        }
    }
    flavorDimensions 'version', 'env'
    productFlavors {
        //noinspection GroovyMissingReturnStatement
        production {
            dimension 'env'
        }

        staging {
            dimension 'env'
            applicationIdSuffix '.staging'
        }

        lollipopAndAbove {
            dimension 'version'
            minSdkVersion 21 // For faster multidex compilation
        }

        allVersions {
            dimension 'version'
        }
    }
    //noinspection GroovyMissingReturnStatement
    lintOptions {
        checkAllWarnings true
        warning 'InvalidPackage'
        warningsAsErrors true
    }
    packagingOptions {
        exclude '.readme'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    androidTestCompile "com.android.support:support-annotations:${libraryVersion.androidSupport}"
    androidTestCompile "com.android.support.test:rules:${libraryVersion.androidSupportTest}"
    androidTestCompile("com.android.support.test:runner:${libraryVersion.androidSupportTest}") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:${libraryVersion.espresso}") {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude module: 'recyclerview-v7'
    }
    androidTestCompile("com.android.support.test.espresso:espresso-core:${libraryVersion.espresso}") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile "com.android.support.test.espresso:espresso-intents:${libraryVersion.espresso}"
    androidTestCompile "com.github.andrzejchm.RESTMock:android:${libraryVersion.restMock}"
    androidTestCompile("com.github.andrzejchm.RESTMock:core:${libraryVersion.restMock}") {
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
    }
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    androidTestCompile "com.crittercism.dexmaker:dexmaker:${libraryVersion.dexmaker}"
    androidTestCompile "com.crittercism.dexmaker:dexmaker-dx:${libraryVersion.dexmaker}"
    androidTestCompile "com.crittercism.dexmaker:dexmaker-mockito:${libraryVersion.dexmaker}"
    androidTestCompile "com.google.code.findbugs:jsr305:${libraryVersion.jsr305}" // Added to prevent dependencies conflict between main and test
    androidTestCompile 'com.jakewharton.espresso:okhttp3-idling-resource:1.0.0'
    androidTestCompile "com.squareup.okhttp3:okhttp:${libraryVersion.okHttp}" // Added to prevent dependencies conflict between main and test
    androidTestCompile "com.squareup.okhttp3:mockwebserver:${libraryVersion.okHttp}" // Added to prevent dependencies conflict between main and test
    androidTestCompile "com.squareup.okio:okio:${libraryVersion.okio}" // Added to prevent dependencies conflict between main and test
    androidTestCompile "org.mockito:mockito-core:${libraryVersion.mockito}"

    compile "com.android.support:appcompat-v7:${libraryVersion.androidSupport}"
    compile "com.android.support:cardview-v7:${libraryVersion.androidSupport}"
    compile "com.android.support:design:${libraryVersion.androidSupport}"
    compile "com.android.support:recyclerview-v7:${libraryVersion.androidSupport}"
    compile "com.android.support:support-v13:${libraryVersion.androidSupport}"
    compile "com.f2prateek.dart:dart:${libraryVersion.dart}"
    compile "com.f2prateek.dart:henson:${libraryVersion.dart}"
    compile 'com.github.mrmike:Ok2Curl:0.2.5'
    compile "com.github.Raizlabs.DBFlow:dbflow-core:${libraryVersion.dbFlow}"
    compile "com.github.Raizlabs.DBFlow:dbflow:${libraryVersion.dbFlow}"
    compile 'com.github.square.picasso:picasso:8c16e8564e'
    compile "com.github.tbroyer.bullet:bullet:${libraryVersion.bullet}"
    compile "com.google.dagger:dagger:${libraryVersion.dagger}"
    compile "com.hannesdorfmann.fragmentargs:annotation:${libraryVersion.fragmentArgs}"
    compile "com.hannesdorfmann.fragmentargs:bundler-parceler:${libraryVersion.fragmentArgs}"
    compile "com.jakewharton.threetenabp:threetenabp:${libraryVersion.localDateBackport}"
    compile "com.jakewharton.timber:timber:${libraryVersion.timber}"
    compile "com.squareup.okhttp3:okhttp:${libraryVersion.okHttp}"
    compile "com.squareup.okhttp3:logging-interceptor:${libraryVersion.okHttp}"
    compile "com.squareup.okio:okio:${libraryVersion.okio}"
    compile "com.squareup.retrofit2:converter-gson:${libraryVersion.retrofit}"
    compile "com.squareup.retrofit2:adapter-rxjava2:${libraryVersion.retrofit}"
    compile "com.squareup.retrofit2:retrofit:${libraryVersion.retrofit}"
    compile "com.trello.rxlifecycle2:rxlifecycle:${libraryVersion.rxLifecycle}"
    compile "com.trello.rxlifecycle2:rxlifecycle-components:${libraryVersion.rxLifecycle}"
    compile 'com.viewpagerindicator:library:2.4.1@aar'
    compile "com.wdullaer:materialdatetimepicker:${libraryVersion.materialDateTimePicker}"
    compile "io.reactivex.rxjava2:rxandroid:${libraryVersion.rxAndroid}"
    compile "io.reactivex.rxjava2:rxjava:${libraryVersion.rxJava}"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:${libraryVersion.kotlin}"
    compile "org.parceler:parceler-api:${libraryVersion.parceler}"
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(path: ':core')
    compile project(path: ':log-crashlytics')
    compile project(path: ':retrofit')
    compile project(path: ':ui')

    kapt "com.f2prateek.dart:dart-processor:${libraryVersion.dart}"
    kapt "com.f2prateek.dart:henson-processor:${libraryVersion.dart}"
    kapt "com.github.Raizlabs.DBFlow:dbflow-processor:${libraryVersion.dbFlow}"
    kapt "com.github.tbroyer.bullet:bullet-compiler:${libraryVersion.bullet}"
    kapt "com.google.dagger:dagger-compiler:${libraryVersion.dagger}"
    kapt "com.hannesdorfmann.fragmentargs:processor:${libraryVersion.fragmentArgs}"
    kapt "org.parceler:parceler:${libraryVersion.parceler}"

    kaptAndroidTest "com.github.tbroyer.bullet:bullet-compiler:$libraryVersion.bullet"
    kaptAndroidTest "com.google.dagger:dagger-compiler:${libraryVersion.dagger}"

    provided 'javax.annotation:jsr250-api:1.0'

    testCompile "junit:junit:${libraryVersion.junit}"
    testCompile ('org.threeten:threetenbp:1.3.6') {
        exclude group:'com.jakewharton.threetenabp', module:'threetenabp'
    }
}

//apply plugin: 'com.google.gms.google-services' // TODO: enable when adding google-services.json
