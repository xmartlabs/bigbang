buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.14.0'
        classpath 'com.google.gms:google-services:3.0.0'
        classpath 'io.fabric.tools:gradle:1.22.1'
    }
}

apply plugin: "io.gitlab.arturbosch.detekt"
apply plugin: 'com.android.application'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply from: '../detekt.gradle'

def getSignInformation = { keystorePath, keystorePropertyPath ->
    def keyStore = file(keystorePath)
    if (!keyStore.exists()) {
        logger.error("Keystore file not found, path=${keyStore.absolutePath}")
        return
    }
    def propertiesFile = file(keystorePropertyPath)
    if (!propertiesFile.exists()) {
        logger.error("Keystore properties file not found, path=${propertiesFile.absolutePath}")
        return
    }
    def Properties properties = new Properties()
    properties.load(new FileInputStream(propertiesFile))
    return [
            storeFile    : keyStore,
            storePassword: properties["store_password"],
            keyAlias     : properties["key_alias"],
            keyPassword  : properties["key_password"]
    ]
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    defaultConfig {
        applicationId "com.xmartlabs.bigbang.template"
        minSdkVersion 19
        multiDexEnabled true
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    dexOptions {
        javaMaxHeapSize '4g'
    }
    signingConfigs {
        //noinspection GroovyMissingReturnStatement
        debug {
            def signInformation = getSignInformation("debug.keystore", "keystore.properties")
            if (signInformation != null) {
                storeFile signInformation.storeFile
                storePassword signInformation.storePassword
                keyAlias signInformation.keyAlias
                keyPassword signInformation.keyPassword
            }
        }
        //noinspection GroovyMissingReturnStatement
        release {
            def signInformation = getSignInformation("release.keystore", "keystore.properties")
            if (signInformation != null) {
                storeFile signInformation.storeFile
                storePassword signInformation.storePassword
                keyAlias signInformation.keyAlias
                keyPassword signInformation.keyPassword
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    flavorDimensions 'version', 'env'
    productFlavors {
        //noinspection GroovyMissingReturnStatement
        production {
            dimension 'env'
        }

        staging {
            dimension 'env'
            applicationIdSuffix '.staging'
        }

        lollipopAndAbove {
            dimension 'version'
            minSdkVersion 21 // For faster multidex compilation
        }

        allVersions {
            dimension 'version'
        }
    }
    //noinspection GroovyMissingReturnStatement
    lintOptions {
        warning 'InvalidPackage'
    }
    packagingOptions {
        exclude '.readme'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

final ANDROID_SUPPORT_VERSION = '26.0.1'
final BULLET_VERSION = 'e5044b53df'
final BIGBANG_VERSION = '75f26d1'
final DAGGER_VERSION = '2.10'
final DART_VERSION = '2.0.2'
final DBFLOW_VERSION = '4.0.2'
final FRAGMENTARGS_VERSION = '3.0.2'
final KOTLIN_VERSION = '1.1.3-2'
final LOCALDATE_BACKPORT_VERSION = '1.0.5'
final MATERIAL_DIALOGS_VERSION = '0.9.4.4'
final MULTIDEX_VERSION = '1.0.1'
final PARCELER_VERSION = '1.1.6'
final RETROFIT_VERSION = '2.2.0'
final RXANDROID_VERSION = '2.0.1'
final RXJAVA_VERSION = '2.0.9'
final RXLIFECYCLE_VERSION = '2.0.1'
final TIMBER_VERSION = '4.5.1'
final TRACEUR_VERSION = '273cc7d582'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile 'com.google.code.findbugs:jsr305:3.0.1'
    compile "com.afollestad.material-dialogs:core:${MATERIAL_DIALOGS_VERSION}"
    compile "com.android.support:appcompat-v7:${ANDROID_SUPPORT_VERSION}"
    compile "com.android.support:design:${ANDROID_SUPPORT_VERSION}"
    compile "com.android.support:support-v13:${ANDROID_SUPPORT_VERSION}"
    compile "com.android.support:multidex:${MULTIDEX_VERSION}"
    compile('com.crashlytics.sdk.android:crashlytics:2.6.7@aar') {
        transitive = true
    }
    compile "com.f2prateek.dart:dart:${DART_VERSION}"
    compile "com.f2prateek.dart:henson:${DART_VERSION}"
    compile "com.github.Raizlabs.DBFlow:dbflow:${DBFLOW_VERSION}"
    compile "com.github.Raizlabs.DBFlow:dbflow-core:${DBFLOW_VERSION}"
    compile "com.github.tbroyer.bullet:bullet:${BULLET_VERSION}"
    compile "com.github.xmartlabs.bigbang:core:${BIGBANG_VERSION}"
    compile "com.github.xmartlabs.bigbang:dbflow:${BIGBANG_VERSION}"
    compile "com.github.xmartlabs.bigbang:log-crashlytics:${BIGBANG_VERSION}"
    compile "com.github.xmartlabs.bigbang:retrofit:${BIGBANG_VERSION}"
    compile "com.github.xmartlabs.bigbang:ui:${BIGBANG_VERSION}"
    compile "com.github.xmartlabs:Traceur:${TRACEUR_VERSION}"
    compile "com.google.dagger:dagger:${DAGGER_VERSION}"
    compile "com.hannesdorfmann.fragmentargs:annotation:${FRAGMENTARGS_VERSION}"
    compile "com.hannesdorfmann.fragmentargs:bundler-parceler:${FRAGMENTARGS_VERSION}"
    compile "com.jakewharton.threetenabp:threetenabp:${LOCALDATE_BACKPORT_VERSION}"
    compile "com.jakewharton.timber:timber:${TIMBER_VERSION}"
    compile "com.squareup.retrofit2:adapter-rxjava2:${RETROFIT_VERSION}"
    compile "com.squareup.retrofit2:retrofit:${RETROFIT_VERSION}"
    compile "com.trello.rxlifecycle2:rxlifecycle:${RXLIFECYCLE_VERSION}"
    compile "com.trello.rxlifecycle2:rxlifecycle-components:${RXLIFECYCLE_VERSION}"
    compile "io.reactivex.rxjava2:rxandroid:${RXANDROID_VERSION}"
    compile "io.reactivex.rxjava2:rxjava:${RXJAVA_VERSION}"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:${KOTLIN_VERSION}"
    compile "org.parceler:parceler:${PARCELER_VERSION}"
    kapt "com.f2prateek.dart:dart-processor:${DART_VERSION}"
    kapt "com.f2prateek.dart:henson-processor:${DART_VERSION}"
    kapt "com.github.tbroyer.bullet:bullet-compiler:${BULLET_VERSION}"
    kapt "com.github.Raizlabs.DBFlow:dbflow-processor:${DBFLOW_VERSION}"
    kapt "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"
    kapt "com.hannesdorfmann.fragmentargs:processor:${FRAGMENTARGS_VERSION}"
    kapt "org.parceler:parceler:${PARCELER_VERSION}"
    provided 'javax.annotation:jsr250-api:1.0'
    testCompile 'junit:junit:4.12'
}
