buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
        classpath 'com.google.gms:google-services:3.1.0'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
        classpath 'io.fabric.tools:gradle:1.23.0'
        classpath 'me.tatarka:gradle-retrolambda:3.7.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'me.tatarka.retrolambda'

def getSignInformation = { keystorePath, keystorePropertyPath ->
    def keyStore = file(keystorePath)
    if (!keyStore.exists()) {
        logger.error("Keystore file not found, path=${keyStore.absolutePath}")
        return
    }
    def propertiesFile = file(keystorePropertyPath)
    if (!propertiesFile.exists()) {
        logger.error("Keystore properties file not found, path=${propertiesFile.absolutePath}")
        return
    }
    def Properties properties = new Properties()
    properties.load(new FileInputStream(propertiesFile))
    return [
            storeFile    : keyStore,
            storePassword: properties["store_password"],
            keyAlias     : properties["key_alias"],
            keyPassword  : properties["key_password"]
    ]
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.xmartlabs.bigbang.template"
        minSdkVersion 19
        multiDexEnabled true
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    dexOptions {
        javaMaxHeapSize '4g'
    }
    signingConfigs {
        //noinspection GroovyMissingReturnStatement
        debug {
            def signInformation = getSignInformation("debug.keystore", "keystore.properties")
            if (signInformation != null) {
                storeFile signInformation.storeFile
                storePassword signInformation.storePassword
                keyAlias signInformation.keyAlias
                keyPassword signInformation.keyPassword
            }
        }
        //noinspection GroovyMissingReturnStatement
        release {
            def signInformation = getSignInformation("release.keystore", "keystore.properties")
            if (signInformation != null) {
                storeFile signInformation.storeFile
                storePassword signInformation.storePassword
                keyAlias signInformation.keyAlias
                keyPassword signInformation.keyPassword
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    flavorDimensions 'version', 'env'
    productFlavors {
        //noinspection GroovyMissingReturnStatement
        production {
            dimension 'env'
        }

        staging {
            dimension 'env'
            applicationIdSuffix '.staging'
        }

        lollipopAndAbove {
            dimension 'version'
            minSdkVersion 21 // For faster multidex compilation
        }

        allVersions {
            dimension 'version'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    //noinspection GroovyMissingReturnStatement
    lintOptions {
        warning 'InvalidPackage'
    }
    packagingOptions {
        exclude '.readme'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

final ANDROID_SUPPORT_VERSION = '25.3.1'
final BULLET_VERSION = 'e5044b53df'
final BUTTERKNIFE_VERSION = '8.8.1'
final BIGBANG_VERSION = '0.1.54'
final DAGGER_VERSION = '2.11'
final DART_VERSION = '2.0.2'
final DBFLOW_VERSION = '4.0.5'
final DRAG_LIST_VIEW_VERSION = 'b49f21d'
final FRAGMENTARGS_VERSION = '3.0.2'
final LOCALDATE_BACKPORT_VERSION = '1.0.5'
final LOMBOK_VERSION = '1.16.18'
final MATERIAL_DIALOGS_VERSION = '0.9.4.5'
final MULTIDEX_VERSION = '1.0.1'
final PARCELER_VERSION = '1.1.9'
final RETROFIT_VERSION = '2.3.0'
final RXANDROID_VERSION = '2.0.1'
final RXJAVA_VERSION = '2.1.2'
final RXLIFECYCLE_VERSION = '2.1.0'
final STREAM_VERSION = '1.1.8'
final TIMBER_VERSION = '4.5.1'
final TRACEUR_VERSION = '273cc7d582'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile 'com.google.code.findbugs:jsr305:3.0.1'
    apt "com.f2prateek.dart:dart-processor:${DART_VERSION}"
    apt "com.f2prateek.dart:henson-processor:${DART_VERSION}"
    apt "com.github.tbroyer.bullet:bullet-compiler:${BULLET_VERSION}"
    apt "com.github.Raizlabs.DBFlow:dbflow-processor:${DBFLOW_VERSION}"
    apt "com.hannesdorfmann.fragmentargs:processor:${FRAGMENTARGS_VERSION}"
    apt "com.jakewharton:butterknife-compiler:${BUTTERKNIFE_VERSION}"
    apt "org.parceler:parceler:${PARCELER_VERSION}"
    apt "org.projectlombok:lombok:${LOMBOK_VERSION}"
    compile "com.afollestad.material-dialogs:core:${MATERIAL_DIALOGS_VERSION}"
    compile "com.android.support:appcompat-v7:${ANDROID_SUPPORT_VERSION}"
    compile "com.android.support:customtabs:${ANDROID_SUPPORT_VERSION}"
    compile "com.android.support:multidex:${MULTIDEX_VERSION}"
    compile "com.annimon:stream:${STREAM_VERSION}"
    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true
    }
    compile "com.f2prateek.dart:dart:${DART_VERSION}"
    compile "com.f2prateek.dart:henson:${DART_VERSION}"
    compile "com.github.Raizlabs.DBFlow:dbflow:${DBFLOW_VERSION}"
    compile "com.github.Raizlabs.DBFlow:dbflow-core:${DBFLOW_VERSION}"
    compile "com.github.tbroyer.bullet:bullet:${BULLET_VERSION}"
    compile "com.github.xmartlabs:Traceur:${TRACEUR_VERSION}"
    compile "com.github.xmartlabs:draglistview:${DRAG_LIST_VIEW_VERSION}"
    compile "com.google.dagger:dagger:${DAGGER_VERSION}"
    compile "com.hannesdorfmann.fragmentargs:annotation:${FRAGMENTARGS_VERSION}"
    compile "com.hannesdorfmann.fragmentargs:bundler-parceler:${FRAGMENTARGS_VERSION}"
    compile "com.jakewharton:butterknife:${BUTTERKNIFE_VERSION}"
    compile "com.jakewharton.threetenabp:threetenabp:${LOCALDATE_BACKPORT_VERSION}"
    compile "com.jakewharton.timber:timber:${TIMBER_VERSION}"
    compile "com.squareup.retrofit2:adapter-rxjava2:${RETROFIT_VERSION}"
    compile "com.squareup.retrofit2:retrofit:${RETROFIT_VERSION}"
    compile "com.trello.rxlifecycle2:rxlifecycle:${RXLIFECYCLE_VERSION}"
    compile "com.trello.rxlifecycle2:rxlifecycle-components:${RXLIFECYCLE_VERSION}"
    compile "com.xmartlabs.bigbang:core:${BIGBANG_VERSION}"
    compile "com.xmartlabs.bigbang:dbflow:${BIGBANG_VERSION}"
    compile "com.xmartlabs.bigbang:log-crashlytics:${BIGBANG_VERSION}"
    compile "com.xmartlabs.bigbang:retrofit:${BIGBANG_VERSION}"
    compile "com.xmartlabs.bigbang:ui:${BIGBANG_VERSION}"
    compile "io.reactivex.rxjava2:rxandroid:${RXANDROID_VERSION}"
    compile "io.reactivex.rxjava2:rxjava:${RXJAVA_VERSION}"
    compile "org.parceler:parceler:${PARCELER_VERSION}"
    provided "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"
    provided "org.projectlombok:lombok:${LOMBOK_VERSION}"
    testCompile 'junit:junit:4.12'
}
